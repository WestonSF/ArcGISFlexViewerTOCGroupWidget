<?xml version="1.0" encoding="utf-8"?>
<viewer:BaseWidget xmlns:fx="http://ns.adobe.com/mxml/2009"
                   xmlns:s="library://ns.adobe.com/flex/spark"
                   xmlns:mx="library://ns.adobe.com/flex/mx"
                   xmlns:viewer="com.esri.viewer.*"
                   xmlns:toccomp="widgets.TOC.toc.*"
				   xmlns:esri="http://www.esri.com/2008/ags"
                   widgetConfigLoaded="init()"
				   mouseDown="wTemplate_clickHandler(event)"
                   x="600" y="400"
				   initialize="basewidget1_initializeHandler(event)">
	
    <fx:Script>
        <![CDATA[
			import com.esri.viewer.AppEvent;
			
			import flash.net.registerClassAlias;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			import widgets.TOC.toc.tocClasses.LegendDataClassItem;
			import widgets.TOC.toc.tocClasses.LegendDataItem;
			import widgets.TOC.toc.tocClasses.LegendSymbolItem;
			
			//Build Constants
			protected const VERSION:String = "3.1.4";
			protected const BUILDDATE:String = "1/25/2013";
			
			//Resources
			[Embed(source="assets/images/i_about.png")]
			private var iconClass:Class;
			
			[Event(name="layervischanged", type="flash.events.Event")]
			private static const LAYER_VIS_CHANGED:String = "layervischanged";
			
            private const ICON_URL:String = "widgets/TOC/assets/images/";
			
			[Embed(source="widgets/TOC/assets/images/plus.png")]
			public var plus:Class;
			
			[Embed(source="widgets/TOC/assets/images/minus.png")]
			public var minus:Class;
			
			[Bindable] private var expanded:Boolean;
			
			[Bindable] private var fullexpand:Boolean;
			
			[Bindable] private var legendCollapsed:Boolean;
			
			[Bindable] private var UseNewEsriDescrption:Boolean;
			
			[Bindable] private var disableZoomTo:Boolean;
			
			//label
			[Bindable] private var layerVisibility:String;
			
			[Bindable]private var lTimeout:int = 60;
			
			[Bindable] private var excludedLayers:ArrayCollection;
			
			private var excludeGraphicsLayers:Boolean = false;
			
			[Bindable] private var ZoomToMakeVisible:String;
			
			[Bindable] private var ExpandAll:String;
			
			[Bindable] private var CollapseAll:String;
			
			[Bindable] private var GeneratingLegendMsg:String;
			

            private function init():void
            {
                if (configXML){
					ZoomToMakeVisible = configXML.labels.zoomtomakevisible || "Zoom To Make Visible";
					ExpandAll = configXML.labels.expandall || "Expand All Layers";
					CollapseAll = configXML.labels.collapseall || "Collapse All Layers";
					layerVisibility = configXML.labels.visibilitylabel || getDefaultString("layerListTitle");;
					GeneratingLegendMsg = configXML.labels.generatinglegendmsg || "Generating TOC Legend";
					expanded = configXML.tocsettings.expanded && configXML.tocsettings.expanded == "true";
					disableZoomTo = configXML.tocsettings.disablezoomtomenuoption && configXML.tocsettings.disablezoomtomenuoption == "true";
					if(expanded)
						fullexpand = configXML.tocsettings.expanded.@everything[0] && configXML.tocsettings.expanded.@everything[0] == "true";
					legendCollapsed = configXML.tocsettings.collapselegends && configXML.tocsettings.collapselegends == "true";
					UseNewEsriDescrption = configXML.usenewesridescription && configXML.usenewesridescription == "true";
                    toc.map = map;
                    toc.isMapServiceOnly = false; //gotta get this from the config file
					toc.excludeGraphicsLayers = configXML.excludegraphiclayers &&  configXML.excludegraphiclayers == "true";
					toc.setStyle("disclosureOpenIcon", minus);
					toc.setStyle("disclosureClosedIcon", plus);
					
					excludedLayers = new ArrayCollection;
					excludeGraphicsLayers = configXML.excludegraphiclayers && configXML.excludegraphiclayers == "true";
					if (configData && configData.basemaps){
						if(configXML.excludebasemaplayers && configXML.excludebasemaplayers == "true"){
							var bname:String;
							// exclude basemaps
							for (var b:int = 0; b < configData.basemaps.length; b++){
								bname = configData.basemaps[b].label;
								var excludesb:Object ={
									name: bname,
									ids: null,
									isbasemap: true
								}
								excludedLayers.addItem(excludesb);
							}
						}
					}
					
					var excludeList:XMLList = configXML..excludelayer;
					for (var i:Number = 0; i < excludeList.length(); i++){
						var name:String = excludeList[i].@mapservice;
						var ids:String = excludeList[i];
						var idsArray:Array = ids.split(",");
						if(idsArray[0] == "")
							idsArray = null;
						var excludes:Object ={
							name: name,
							ids: idsArray,
							isbasemap: false
						}
						excludedLayers.addItem(excludes);
					}
                }
				toc.excludeLayers = excludedLayers;
				wTemplate.header.addEventListener(MouseEvent.CLICK, DisplayVersion);
            }

			private function getExcludeLayers():ArrayCollection
			{
				var result:ArrayCollection = new ArrayCollection();
				
				if (configData && configData.basemaps){
					if(configXML.excludebasemaplayers &&  configXML.excludebasemaplayers == "true"){
						// exclude basemaps
						for (var i:int = 0; i < configData.basemaps.length; i++){
							result.addItem(configData.basemaps[i].label);
						}
					}
				}
				
				if (configXML){
					// exclude these layers
					var layers:XMLList = configXML..excludelayer as XMLList;
					for (var j:Number = 0; j < layers.length(); j++){
						result.addItem(layers[j].toString());
					}
				}
				return result;
			}
			
			protected function wTemplate_clickHandler(event:MouseEvent):void
			{
				AppEvent.dispatch(AppEvent.TOC_HIDDEN); // always hide the layer options popup
			}
			
			protected function basewidget1_initializeHandler(event:FlexEvent):void
			{
				registerClassAlias("widgets.TOC.toc.tocClasses.LegendDataClassItem", LegendDataClassItem);
				registerClassAlias("widgets.TOC.toc.tocClasses.LegendDataItem", LegendDataItem);
				registerClassAlias("widgets.TOC.toc.tocClasses.LegendSymbolItem", LegendSymbolItem);
			}
			
			private function DisplayVersion(evt:MouseEvent):void
			{
				if(evt.altKey){
					Alert.show("Table Of Contents Widget Version: " + VERSION + "\nBuild Date: " + BUILDDATE,
						wTemplate.widgetTitle, 4, null, null, iconClass);
				}
			}
			
		]]>
    </fx:Script>
    <viewer:WidgetTemplate id="wTemplate"
						   height="300"
						   skinClass="com.esri.viewer.skins.WidgetTemplateSkin"
						   width="380">
        <s:VGroup id="showTOC"
				  visible="true"
				  gap="8"
                  height="100%"
                  paddingTop="4"
                  width="100%">
			<s:Label styleName="WidgetText" text="{layerVisibility}" paddingLeft="6"/>
			<s:Scroller id="scr" width="100%" height="100%">
				<s:Group>
					<s:layout>
						<s:VerticalLayout gap="1"/>
					</s:layout>
					<s:HGroup id="boxMessage"
							  width="100%"
							  includeInLayout="false"
							  visible="false" 
							  verticalAlign="middle"
							  horizontalAlign="center">
						<s:SWFLoader id="swfMessage"
								  source="widgets/TOC/assets/images/loader.swf"/>
						<s:Label id="txtMessage"
								 width="90%"
								 text="{GeneratingLegendMsg}"/>
					</s:HGroup>
					<toccomp:TOC id="toc"
								 width="100%"
								 height="100%"
								 labels="{[ZoomToMakeVisible,ExpandAll,CollapseAll]}"
								 useesridescription="{UseNewEsriDescrption}"
								 expanded="{expanded}"
								 disableZoomTo="{disableZoomTo}"
								 fullexpanded="{fullexpand}"
								 legendCollapsed="{legendCollapsed}"
								 liveScrolling="true"
								 variableRowHeight="true"
								 tocMinWidth="{wTemplate.width - 45}"
								 scroller="{scr}"
								 loader="{boxMessage}" paddingTop="0" paddingBottom="0"/>
				</s:Group>
			</s:Scroller>
    </s:VGroup>
    </viewer:WidgetTemplate>
</viewer:BaseWidget>